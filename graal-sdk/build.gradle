// Global variables
ext {
    // Directories and files paths
    graalSDKDir = "$graalDir/sdk"
    graalSDKJar = "$graalSDKDir/mxbuild/dists/graal-sdk.jar"
}

// Tasks
task buildGraalSDK(type: Exec) {
    dependsOn(rootProject.patchGraal)

    // Task details
    group = 'Other'
    description = 'Generate graal-sdk.jar file.'

    // Set the input to the repository patch dummy file
    inputs.file(rootProject.graalPatchDummy)
    outputs.file(graalSDKJar)

    doFirst {
        commandLine "${environment.MX_HOME}/mx", "-p", "$graalSDKDir", "build"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

// Maven-publish - Configuration for publishing maven artifacts
publishing {

    // List of artifacts to publish
    publications {

        // Publish the tar file that contains Graal
        maven(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar

            // Artifact to be published
            artifact(graalSDKJar) {
                builtBy(buildGraalSDK)

                // Details about the artifact being published
                artifactId = "graal-sdk"
                description = 'Graal SDK'
            }

            pom {
                licenses {
                    license {
                        name = 'Universal Permissive License, Version 1.0'
                        url = 'https://opensource.org/licenses/UPL'
                    }
                }
            }
        }
    }
}
